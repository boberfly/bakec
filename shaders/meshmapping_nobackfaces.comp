#version 430 core
#extension GL_ARB_compute_shader : enable
#extension GL_ARB_shader_storage_buffer_object : enable
#extension GL_ARB_gpu_shader_fp64 : enable

layout (local_size_x = 64) in;

#define FLT_MAX 3.402823466e+38
#define BARY_MIN -1e-5
#define BARY_MAX 1.0

struct Pix
{
	vec3 p;
	vec3 d;
};

struct BVH
{
	float aabbMinX; float aabbMinY; float aabbMinZ;
	float aabbMaxX; float aabbMaxY; float aabbMaxZ;
	uint start;
	uint end;
	uint jump; // Index to the next BVH if we skip this subtree
};

layout(location = 1) uniform uint workOffset;
layout(location = 2) uniform uint workCount;
layout(location = 3) uniform uint bvhCount;
layout(std430, binding = 4) readonly buffer pixBuffer { Pix pixels[]; };
layout(std430, binding = 5) readonly buffer meshPBuffer { vec3 positions[]; };
layout(std430, binding = 6) readonly buffer bvhBuffer { BVH bvhs[]; };
layout(std430, binding = 7) writeonly buffer rCoordBuffer { vec4 r_coords[]; };
layout(std430, binding = 8) writeonly buffer rTidxBuffer { uint r_tidx[]; };

float RayAABB(vec3 o, vec3 d, vec3 mins, vec3 maxs)
{
	vec3 dabs = abs(d);
	vec3 t1 = (mins - o) / d;
	vec3 t2 = (maxs - o) / d;
	vec3 tmin = min(t1, t2);
	vec3 tmax = max(t1, t2);
	float a = max(tmin.x, max(tmin.y, tmin.z));
	float b = min(tmax.x, min(tmax.y, tmax.z));
	return (b >= 0 && a <= b) ? a : FLT_MAX;
}

vec3 barycentric(dvec3 p, dvec3 a, dvec3 b, dvec3 c)
{
	dvec3 v0 = b - a;
	dvec3 v1 = c - a;
	dvec3 v2 = p - a;
	double d00 = dot(v0, v0);
	double d01 = dot(v0, v1);
	double d11 = dot(v1, v1);
	double d20 = dot(v2, v0);
	double d21 = dot(v2, v1);
	double denom = d00 * d11 - d01 * d01;
	double y = (d11 * d20 - d01 * d21) / denom;
	double z = (d00 * d21 - d01 * d20) / denom;
	return vec3(dvec3(1.0 - y - z, y, z));
}

vec4 raycast(vec3 o, vec3 d, vec3 a, vec3 b, vec3 c, float mindist, float maxdist)
{
	vec3 n = normalize(cross(b - a, c - a));
	float nd = dot(d, n);
	if (nd > 0)
	{
		float pn = dot(o, n);
		float t = (dot(a, n) - pn) / nd;
		if (t >= mindist && t < maxdist)
		{
			vec3 p = o + d * t;
			vec3 b = barycentric(p, a, b, c);
			if (b.x >= BARY_MIN && b.y >= BARY_MIN && b.y <= BARY_MAX && b.z >= BARY_MIN && b.z <= BARY_MAX)
			{
				return vec4(t, b.x, b.y, b.z);
			}
		}
	}
	return vec4(FLT_MAX, 0, 0, 0);
}

void raycastRange(vec3 o, vec3 d, uint start, uint end, float mindist, in out float curdist, in out uint o_idx, in out vec3 o_bcoord)
{
	for (uint tidx = start; tidx < end; tidx += 3)
	{
		vec3 v0 = positions[tidx + 0];
		vec3 v1 = positions[tidx + 1];
		vec3 v2 = positions[tidx + 2];
		vec4 r = raycast(o, d, v0, v1, v2, mindist, curdist);
		if (r.x != FLT_MAX)
		{
			curdist = r.x;
			o_idx = tidx;
			o_bcoord = r.yzw;
		}
	}
}

void raycastBVH(vec3 o, vec3 d, in out float curdist, in out uint o_idx, in out vec3 o_bcoord)
{
	uint i = 0;
	while (i < bvhCount)
	{
		BVH bvh = bvhs[i];
		vec3 aabbMin = vec3(bvh.aabbMinX, bvh.aabbMinY, bvh.aabbMinZ);
		vec3 aabbMax = vec3(bvh.aabbMaxX, bvh.aabbMaxY, bvh.aabbMaxZ);
		float distAABB = RayAABB(o, d, aabbMin, aabbMax);
		if (distAABB < curdist)
		{
			raycastRange(o, d, bvh.start, bvh.end, 0, curdist, o_idx, o_bcoord);
			++i;
		}
		else
		{
			i = bvh.jump;
		}
	}
}

vec4 raycastBack(vec3 o, vec3 d, vec3 a, vec3 b, vec3 c, float mindist, float maxdist)
{
	vec3 n = normalize(cross(b - a, c - a));
	float nd = dot(d, n);
	if (nd < 0)
	{
		float pn = dot(o, n);
		float t = (dot(a, n) - pn) / nd;
		if (t >= mindist && t < maxdist)
		{
			vec3 p = o + d * t;
			vec3 b = barycentric(p, a, b, c);
			if (b.x >= BARY_MIN && b.y >= BARY_MIN && b.y <= BARY_MAX && b.z >= BARY_MIN && b.z <= BARY_MAX)
			{
				return vec4(t, b.x, b.y, b.z);
			}
		}
	}
	return vec4(FLT_MAX, 0, 0, 0);
}

void raycastBackRange(vec3 o, vec3 d, uint start, uint end, float mindist, in out float curdist, in out uint o_idx, in out vec3 o_bcoord)
{
	for (uint tidx = start; tidx < end; tidx += 3)
	{
		vec3 v0 = positions[tidx + 0];
		vec3 v1 = positions[tidx + 1];
		vec3 v2 = positions[tidx + 2];
		vec4 r = raycastBack(o, d, v0, v1, v2, mindist, curdist);
		if (r.x != FLT_MAX)
		{
			curdist = r.x;
			o_idx = tidx;
			o_bcoord = r.yzw;
		}
	}
}

void raycastBackBVH(vec3 o, vec3 d, in out float curdist, in out uint o_idx, in out vec3 o_bcoord)
{
	uint i = 0;
	while (i < bvhCount)
	{
		BVH bvh = bvhs[i];
		vec3 aabbMin = vec3(bvh.aabbMinX, bvh.aabbMinY, bvh.aabbMinZ);
		vec3 aabbMax = vec3(bvh.aabbMaxX, bvh.aabbMaxY, bvh.aabbMaxZ);
		float distAABB = RayAABB(o, d, aabbMin, aabbMax);
		if (distAABB < curdist)
		{
			raycastBackRange(o, d, bvh.start, bvh.end, 0, curdist, o_idx, o_bcoord);
			++i;
		}
		else
		{
			i = bvh.jump;
		}
	}
}

void main()
{
	uint gid = gl_GlobalInvocationID.x + workOffset;
	if (gid >= workCount) return;

	Pix pix = pixels[gid];
	vec3 p = pix.p;
	vec3 d = pix.d;

	uint tidx = 4294967295;
	vec3 bcoord = vec3(0, 0, 0);
	float t = FLT_MAX;
	raycastBVH(p, d, t, tidx, bcoord);
	raycastBackBVH(p, -d, t, tidx, bcoord);

	r_coords[gid] = vec4(t, bcoord.x, bcoord.y, bcoord.z);
	r_tidx[gid] = tidx;
}
